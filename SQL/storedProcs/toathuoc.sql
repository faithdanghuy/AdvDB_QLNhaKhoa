/* 21HTTT1 - 21CLC1.CSDLNC.03
 * 21127004 - Trần Nguyễn An Phong
 * 21127135 - Diệp Hữu Phúc
 * 21127296 - Đặng Hà Huy
 * 21127315 - Nguyễn Gia Khánh
 * 21127712 - Lê Quang Trường
 */
USE [NC03_QLNhaKhoa]
GO

CREATE OR ALTER PROC USP_TOATHUOC_INS
    @IDHOSO VARCHAR(5),
    @IDKEHOACH VARCHAR(5),
    @IDTHUOC VARCHAR(5),
    @SOLUONG INT = 1
AS BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM KEHOACHDIEUTRI
        WHERE IDHOSO = @IDHOSO AND IDKEHOACH = @IDKEHOACH) BEGIN
        RAISERROR('INVALID KEHOACHDIEUTRI', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    IF NOT EXISTS (SELECT * FROM THUOC WHERE IDTHUOC = @IDTHUOC) BEGIN
        RAISERROR('INVALID IDTHUOC', 16, 1)
        ROLLBACK TRAN
        RETURN -2
    END

    IF EXISTS (SELECT * FROM TOATHUOC WHERE IDHOSO = @IDHOSO
        AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC) BEGIN
        RAISERROR('TOATHUOC ALREADY EXISTS', 16, 1)
        ROLLBACK TRAN
        RETURN -3
    END

    DECLARE @COST INT
    SET @COST = (SELECT GIATIEN FROM THUOC
        WHERE IDTHUOC = @IDTHUOC) * @SOLUONG

    INSERT INTO TOATHUOC
    VALUES (@IDHOSO, @IDKEHOACH, @IDTHUOC, @SOLUONG)

    UPDATE KEHOACHDIEUTRI SET PHIDIEUTRI += @COST
    WHERE IDHOSO = @IDHOSO AND IDKEHOACH = @IDKEHOACH
COMMIT TRAN
RETURN 0
GO

CREATE OR ALTER PROC USP_TOATHUOC_UPD
    @IDHOSO VARCHAR(5),
    @IDKEHOACH VARCHAR(5),
    @IDTHUOC VARCHAR(5),
    @SOLUONG INT = 1
AS BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM TOATHUOC WHERE IDHOSO = @IDHOSO
        AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC) BEGIN
        RAISERROR('INVALID TOATHUOC', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    DECLARE @OLDCOST INT, @COST INT
    SET @OLDCOST = (SELECT PHIDIEUTRI FROM KEHOACHDIEUTRI
        WHERE IDHOSO = @IDHOSO AND IDKEHOACH = @IDKEHOACH)
        - (SELECT GIATIEN FROM THUOC WHERE IDTHUOC = @IDTHUOC)
        * (SELECT SOLUONG FROM TOATHUOC WHERE IDHOSO = @IDHOSO
            AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC)

    SET @COST = (SELECT GIATIEN FROM THUOC
        WHERE IDTHUOC = @IDTHUOC) * @SOLUONG

    UPDATE TOATHUOC SET SOLUONG = @SOLUONG WHERE IDHOSO = @IDHOSO
        AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC

    UPDATE KEHOACHDIEUTRI SET PHIDIEUTRI = @OLDCOST + @COST
    WHERE IDHOSO = @IDHOSO AND IDKEHOACH = @IDKEHOACH
COMMIT TRAN
RETURN 0
GO

CREATE OR ALTER PROC USP_TOATHUOC_DEL
    @IDHOSO VARCHAR(5),
    @IDKEHOACH VARCHAR(5),
    @IDTHUOC VARCHAR(5)
AS BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM TOATHUOC WHERE IDHOSO = @IDHOSO
        AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC) BEGIN
        RAISERROR('INVALID TOATHUOC', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    DECLARE @COST INT
    SET @COST = (SELECT GIATIEN FROM THUOC WHERE IDTHUOC = @IDTHUOC)
        * (SELECT SOLUONG FROM TOATHUOC WHERE IDHOSO = @IDHOSO
            AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC)

    DELETE FROM TOATHUOC WHERE IDHOSO = @IDHOSO
        AND IDKEHOACH = @IDKEHOACH AND IDTHUOC = @IDTHUOC

    UPDATE KEHOACHDIEUTRI SET PHIDIEUTRI -= @COST
    WHERE IDHOSO = @IDHOSO AND IDKEHOACH = @IDKEHOACH
COMMIT TRAN
RETURN 0
GO