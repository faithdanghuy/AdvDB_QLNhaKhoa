/* 21HTTT1 - 21CLC1.CSDLNC.03
 * 21127004 - Trần Nguyễn An Phong
 * 21127135 - Diệp Hữu Phúc
 * 21127296 - Đặng Hà Huy
 * 21127315 - Nguyễn Gia Khánh
 * 21127712 - Lê Quang Trường
 */
USE [NC03_QLNhaKhoa]
GO

CREATE OR ALTER PROC USP_KEHOACHDIEUTRI_INS
    @IDHOSO VARCHAR(5),
    @MOTA NVARCHAR(100),
    @NGAYDIEUTRI DATE,
    @GHICHU NVARCHAR(100),
    @LOAITHANHTOAN INT,
    @IDNHASI VARCHAR(5),
    @IDTROKHAM VARCHAR(5),
    @IDLIEUTRINH VARCHAR(5),
    @IDKEHOACH VARCHAR(5) = NULL OUTPUT
AS BEGIN TRAN
    IF NOT EXISTS (SELECT * FROM HOSOBENHNHAN WHERE IDHOSO = @IDHOSO) BEGIN
        RAISERROR('INVALID IDHOSO', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    IF @LOAITHANHTOAN != 0 AND @LOAITHANHTOAN != 1 BEGIN
        RAISERROR('INVALID LOAITHANHTOAN, MUST BE 0 OR 1', 16, 1)
        ROLLBACK TRAN
        RETURN -2
    END

    IF NOT EXISTS (SELECT * FROM TAIKHOAN WHERE IDTAIKHOAN = @IDNHASI) BEGIN
        RAISERROR('INVALID IDNHASI', 16, 1)
        ROLLBACK TRAN
        RETURN -3
    END

    IF @IDTROKHAM IS NOT NULL AND NOT EXISTS (SELECT * FROM TAIKHOAN
        WHERE IDTAIKHOAN = @IDTROKHAM) BEGIN
        RAISERROR('INVALID IDTROKHAM', 16, 1)
        ROLLBACK TRAN
        RETURN -4
    END

    DECLARE @PHIDIEUTRI INT
    SELECT @PHIDIEUTRI = GIA FROM LIEUTRINH WHERE IDLIEUTRINH = @IDLIEUTRINH

    IF @PHIDIEUTRI IS NULL BEGIN
        RAISERROR('INVALID IDLIEUTRINH', 16, 1)
        ROLLBACK TRAN
        RETURN -5
    END

    SELECT @IDKEHOACH = IDKEHOACH FROM KEHOACHDIEUTRI
    WHERE IDKEHOACH = (SELECT MAX(IDKEHOACH) FROM KEHOACHDIEUTRI
        WHERE IDHOSO = @IDHOSO)

    SET @IDKEHOACH = dbo.F_MAKE_ID('KH', @IDKEHOACH)

    INSERT INTO KEHOACHDIEUTRI
    VALUES (@IDHOSO, @IDKEHOACH, @MOTA, @NGAYDIEUTRI, @GHICHU, 0,
        @PHIDIEUTRI, @LOAITHANHTOAN, @IDNHASI, @IDTROKHAM, @IDLIEUTRINH, NULL)
COMMIT TRAN
RETURN 0
GO