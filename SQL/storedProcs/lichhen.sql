/* 21HTTT1 - 21CLC1.CSDLNC.03
 * 21127004 - Trần Nguyễn An Phong
 * 21127135 - Diệp Hữu Phúc
 * 21127296 - Đặng Hà Huy
 * 21127315 - Nguyễn Gia Khánh
 * 21127712 - Lê Quang Trường
 */
USE [NC03_QLNhaKhoa]
GO

CREATE OR ALTER PROC USP_LICHHEN_INS
    @IDHOSO CHAR(7),
    @NGAY DATE,
    @GIO INT,
    @TINHTRANG INT, -- 0: CUỘC HẸN MỚI, 1: TÁI KHÁM
    @GHICHU NVARCHAR(200),
    @IDPHONGKHAM CHAR(7),
    @IDNHASI CHAR(7),
    @IDTROKHAM CHAR(7) = NULL,
    @IDNHANVIENDAT CHAR(7),
    @IDLICHHEN CHAR(7) = NULL OUTPUT
AS BEGIN TRAN
    IF NOT EXISTS (SELECT IDHOSO FROM HOSOBENHNHAN
        WHERE IDHOSO = @IDHOSO) BEGIN
        RAISERROR('INVALID IDHOSO', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    IF NOT EXISTS (SELECT IDPHONGKHAM FROM PHONGKHAM
        WHERE IDPHONGKHAM = @IDPHONGKHAM
            AND GIOMOCUA <= @GIO AND @GIO <= GIODONGCUA) BEGIN
        RAISERROR('INVALID GIO FOR INPUTTED PHONGKHAM, OR INVALID PHONGKHAM', 16, 1)
        ROLLBACK TRAN
        RETURN -2
    END

    IF @TINHTRANG != 0 AND @TINHTRANG != 1 BEGIN
        RAISERROR('INVALID TINHTRANG', 16, 1)
        ROLLBACK TRAN
        RETURN -3
    END

    IF NOT EXISTS (SELECT IDTAIKHOAN FROM TAIKHOAN
        WHERE IDTAIKHOAN = @IDNHASI) BEGIN
        RAISERROR('INVALID IDNHASI', 16, 1)
        ROLLBACK TRAN
        RETURN -4
    END

    IF dbo.F_CHK_NHASI_FREE(@IDNHASI, @NGAY, @GIO) = 0 BEGIN
        RAISERROR('NHASI IS NOT AVAILABLE AS SAID NGAY AND GIO', 16, 1)
        ROLLBACK TRAN
        RETURN -5
    END
    
    IF @IDTROKHAM IS NOT NULL AND NOT EXISTS
        (SELECT * FROM TAIKHOAN WHERE IDTAIKHOAN = @IDTROKHAM) BEGIN
        RAISERROR('INVALID IDTROKHAM', 16, 1)
        ROLLBACK TRAN
        RETURN -6
    END

    IF @IDNHANVIENDAT IS NOT NULL AND NOT EXISTS
        (SELECT * FROM TAIKHOAN WHERE IDTAIKHOAN = @IDNHANVIENDAT) BEGIN
        RAISERROR('INVALID IDNHANVIENDAT', 16, 1)
        ROLLBACK TRAN
        RETURN -7
    END

    SELECT @IDLICHHEN = IDLICHHEN FROM LICHHEN
    WHERE IDLICHHEN = (SELECT MAX(IDLICHHEN) FROM LICHHEN
        WHERE IDHOSO = @IDHOSO)

    SET @IDLICHHEN = dbo.F_MAKE_ID('LH', @IDLICHHEN)

    INSERT INTO LICHHEN
    VALUES (@IDHOSO, @IDLICHHEN, @NGAY, @GIO, @TINHTRANG, @GHICHU,
        @IDPHONGKHAM, @IDNHASI, @IDTROKHAM, @IDNHANVIENDAT)
COMMIT TRAN
GO

CREATE OR ALTER PROC USP_LICHHEN_UPD
    @IDHOSO CHAR(7),
    @IDLICHHEN CHAR(7),
    @NGAY DATE,
    @GIO INT,
    @TINHTRANG INT, -- 0: CUỘC HẸN MỚI, 1: TÁI KHÁM
    @GHICHU NVARCHAR(200),
    @IDPHONGKHAM CHAR(7),
    @IDNHASI CHAR(7),
    @IDTROKHAM CHAR(7),
    @IDNHANVIENDAT CHAR(7),
    @NGUOIUPDATE CHAR(7)
AS BEGIN TRAN
    DECLARE @OLDNGAY DATE, @OLDGIO INT
    SELECT @OLDNGAY = NGAY, @OLDGIO = GIO FROM LICHHEN
    WHERE IDHOSO = @IDHOSO AND IDLICHHEN = @IDLICHHEN

    IF @OLDNGAY IS NULL OR @OLDGIO IS NULL BEGIN
        RAISERROR('INVALID LICHHEN', 16, 1)
        ROLLBACK TRAN
        RETURN -1
    END

    IF NOT EXISTS (SELECT IDPHONGKHAM FROM PHONGKHAM
        WHERE IDPHONGKHAM = @IDPHONGKHAM
            AND GIOMOCUA <= @GIO AND @GIO <= GIODONGCUA) BEGIN
        RAISERROR('INVALID GIO FOR INPUTTED PHONGKHAM, OR INVALID PHONGKHAM', 16, 1)
        ROLLBACK TRAN
        RETURN -2
    END

    IF @TINHTRANG != 0 AND @TINHTRANG != 1 BEGIN
        RAISERROR('INVALID TINHTRANG', 16, 1)
        ROLLBACK TRAN
        RETURN -3
    END

    IF NOT EXISTS (SELECT IDTAIKHOAN FROM TAIKHOAN
        WHERE IDTAIKHOAN = @IDNHASI) BEGIN
        RAISERROR('INVALID IDNHASI', 16, 1)
        ROLLBACK TRAN
        RETURN -4
    END

    IF @OLDNGAY != @NGAY OR @OLDGIO != @GIO
        IF dbo.F_CHK_NHASI_FREE(@IDNHASI, @NGAY, @GIO) = 0 BEGIN
            RAISERROR('NHASI IS NOT AVAILABLE AS SAID NGAY AND GIO', 16, 1)
            ROLLBACK TRAN
            RETURN -5
        END
    
    IF @IDTROKHAM IS NOT NULL AND NOT EXISTS
        (SELECT IDTAIKHOAN FROM TAIKHOAN WHERE IDTAIKHOAN = @IDTROKHAM) BEGIN
        RAISERROR('INVALID IDTROKHAM', 16, 1)
        ROLLBACK TRAN
        RETURN -6
    END

    IF NOT EXISTS (SELECT IDTAIKHOAN FROM TAIKHOAN
        WHERE IDTAIKHOAN = @IDNHANVIENDAT) BEGIN
        RAISERROR('INVALID IDNHANVIENDAT', 16, 1)
        ROLLBACK TRAN
        RETURN -7
    END

    IF NOT EXISTS (SELECT IDTAIKHOAN FROM TAIKHOAN
        WHERE IDTAIKHOAN = @NGUOIUPDATE) BEGIN
        RAISERROR('INVALID NGUOIUPDATE', 16, 1)
        ROLLBACK TRAN
        RETURN -6
    END

    SET @IDNHANVIENDAT = @NGUOIUPDATE

    UPDATE LICHHEN SET NGAY = @NGAY, GIO = @GIO, TINHTRANG = @TINHTRANG,
        GHICHU = @GHICHU, IDPHONGKHAM = @IDPHONGKHAM, IDNHASI = @IDNHASI,
        IDTROKHAM = @IDTROKHAM, IDNHANVIENDAT = @IDNHANVIENDAT
    WHERE IDHOSO = @IDHOSO AND IDLICHHEN = @IDLICHHEN
COMMIT TRAN
RETURN 0
GO